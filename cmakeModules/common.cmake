set_property(GLOBAL PROPERTY USE_FOLDERS ON)

function(get_all_targets _result _dir)
    get_property(_subdirs DIRECTORY ${_dir} PROPERTY SUBDIRECTORIES)
    foreach(_subdir IN LISTS _subdirs)
        get_all_targets(${_result} ${_subdir})
    endforeach()
    get_property(_sub_targets DIRECTORY ${_dir} PROPERTY BUILDSYSTEM_TARGETS)
    set(${_result} ${${_result}} ${_sub_targets} PARENT_SCOPE)
endfunction()

function(subdir_to_folder _dir _folder)
    get_all_targets(_targets ${_dir})
    set_property(TARGET ${_targets} PROPERTY FOLDER ${_folder})
endfunction()

function(subdir_to_folder_prepend _dir _prependFolder)
    get_all_targets(_allTargets ${_dir})
    foreach (_target IN LISTS _allTargets)
        get_target_property(_folder ${_target} FOLDER)
        if (NOT _folder)
            set (_folder ${_prependFolder})
        else()
            string(PREPEND _folder "${_prependFolder}/")
        endif()
        set_property(TARGET ${_target} PROPERTY FOLDER ${_folder})
    endforeach()
endfunction()